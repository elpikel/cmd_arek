<!DOCTYPE html>
<html lang="en">

    <head>
        <meta charset="utf-8">
        <title>cmdarek.com - Using LiveView and Genservers to track cryptocurrency exchanges</title>

        <link rel="preconnect" href="https://fonts.gstatic.com">
        <link href="https://fonts.googleapis.com/css2?family=Bungee+Outline&family=Roboto+Mono:wght@500&family=Roboto:wght@400;500&display=swap" rel="stylesheet">
        <link rel="stylesheet" href="https://cdnjs.cloudflare.com/ajax/libs/font-awesome/4.7.0/css/font-awesome.min.css">

        <link rel="stylesheet" href="../styles/hybrid.css">
        <script src="../js/highlight.pack.js"></script>
        <script>
            hljs.initHighlightingOnLoad();
        </script>

        <meta name="viewport" content="width=device-width, initial-scale=1">
        <meta name="Description" lang="en" content="">
        <meta name="author" content="">
        <meta name="robots" content="index, follow">

        <link rel="apple-touch-icon" href="../images/logo.svg">
        <link rel="shortcut icon" href="../images/logo.svg">
        <link rel="icon" href="images/logo.svg">

        <link rel="stylesheet" href="../styles/general.css">
    </head>

    <body>
        <div class="container">
            <div class="header">
                <a href="../index.html">
                    <img src="../images/logo.svg" alt="logo" />
                    <h1>cmdarek</h1>
                </a>
                <div>
                    <a target="_blank" href="https://twitter.com/el_pikel" class="fa fa-twitter"></a>
                    <a target="_blank" href="https://www.linkedin.com/in/arkadiusz-plichta-97a7a04b/" class="fa fa-linkedin"></a>
                </div>
            </div>
            <!--<div class="nav-bar">
                <ul class="nav">
                <li><a href="#">Nav item 1</a></li>
                </ul>
                </div> -->
                <div class="content">
                    <div class="main">
                        <span>01-02-2021</span>
                        <h2>Using LiveView and GenServers to track cryptocurrency exchanges</h2>

                        <p>My assumption was that price of Bitcoin varies from one cryptocurrecy exchange to another and that I could make $$$ just by buying and selling BTC on different platforms. In order to check that I could just get prices from last few days from few exchanges and use Excel to do the comparison or I could use this idea to improve my knowledge on LiveView and GenServers. As you have already guessed I picked latter option.</p>

                        <p><img src="../images/ebisu.svg" class="center"/></p>

                        <p>Our solution consist of two application: ebisu and ebisu_web. First one is responsible for periodically check BTC price in exchanges, saving fetched data in db and notifying ebisu_web about new pices. Ebisu_web is simple web application that shows BTC price from few exchanges in real time. To not add unnecessary complexity I'm going to describe solution only for bitbay.<p/>

                        <p><img src="../images/ebisu.gif" class="center"/></p>

                        <h3>Main components</h3>
                        <p>To fetch data from exchanges we use <a href="https://github.com/edgurgel/httpoison">HTTPoison</a> which is simple yet powerfull HTTP client. Ebisu.Utils.Http is wrapper around this library. It is done that way to reuse it in multiple places and to mock it in test. Ebisu.Bitbay.Clients.Http provides us with function to get data from exchange and exchange rate provider. We use it to calculate USD to PLN price.</p>                       
                        <pre>
                        <code class="elixir">
defmodule Ebisu.Utils.Http do
  @timeout 5000
  @callback get(String.t()) :: term() | no_return() | {:error, term()}

  def get(url) do
    case HTTPoison.get(url, [{"content-type", "application/json"}], recv_timeout: @timeout) do
      {:ok, %HTTPoison.Response{body: body, status_code: 200}} ->
        Jason.decode!(body)

      {:ok, %HTTPoison.Response{body: body}} ->
        {:error, body}

      {:error, %HTTPoison.Error{reason: reason}} ->
        {:error, reason}
    end
  end

  def new do
    Application.get_env(:ebisu, :http_client)
  end
end
                        </code>
                        </pre>
                        <p>To simplify code we have another layer of abstraction for http client. For each exchange we have specific http client. In BitBay example we are going to fetch BTC price in PLN which we then convert to USD.</p>
                        <pre>
                        <code class="elixir">
defmodule Ebisu.Bitbay.Clients.Http do
  alias Ebisu.Utils.Http

  def get_ticker do
    Http.new().get("https://bitbay.net/API/Public/BTCPLN/ticker.json")
  end

  def get_rate do
    rate = Http.new().get("http://api.nbp.pl/api/exchangerates/rates/a/usd/?format=json")

    rate
    |> Map.get("rates")
    |> Enum.at(0)
    |> Map.get("mid")
  end
end
                        </code> 
                        </pre>

                        <p>In heart of our application is exchange module which main responsibility is to get last price of BTC, converte it to USD and save it to db. Data is going to be stored in PostgreSQL using Ecto. For this to work we also have to define schema.</p>
                        <pre>
                        <code>
defmodule Ebisu.Bitbay do
  alias Ebisu.Bitbay.Clients.Http
  alias Ebisu.Bitbay.Ticker

  alias Ebisu.Repo

  import Ecto.Query

  def add_ticker do
    rate = Http.get_rate()

    Http.get_ticker()
    |> Map.put("rate", rate)
    |> Ticker.changeset()
    |> Repo.insert()
  end

  def tickers(limit \\ 20) do
    Ticker
    |> order_by(desc: :updated_at)
    |> limit(^limit)
    |> Repo.all()
  end
end
                        </code>
                        <code>
defmodule Ebisu.Bitbay.Ticker do
  use Ecto.Schema

  import Ecto.Changeset

  @fields [:max, :min, :last, :bid, :ask, :vwap, :average, :volume, :rate]

  schema "bitbay_tickers" do
    field(:max, :float)
    field(:min, :float)
    field(:last, :float)
    field(:bid, :float)
    field(:ask, :float)
    field(:vwap, :float)
    field(:average, :float)
    field(:volume, :float)
    field(:rate, :float)

    timestamps(type: :utc_datetime)
  end

  def changeset(params) do
    %__MODULE__{}
    |> cast(params, @fields)
    |> validate_required(@fields)
  end
end
                        </code></pre>

                        <p>Last part of our ebisu application is GenServer which we use to periodically call exchange module and broadcast information to all subscibers using phoenix pub_sub. Subscriber to bitbay meassages with ticker information is LiveView which is part of ebisu_web.</p>
                        <pre>
                        <code>
defmodule Ebisu.Bitbay.Worker.Ticker do
  use GenServer

  alias Ebisu.Bitbay

  @interval Application.fetch_env!(:ebisu, :exchange_worker_interval)

  def start_link(state \\ []) do
    GenServer.start_link(__MODULE__, state)
  end

  def init(state) do
    schedule_ticker_add(state)

    {:ok, state}
  end

  def handle_info(:add_ticker, state) do
    schedule_ticker_add(state)

    {:ok, ticker} = Bitbay.add_ticker()

    Phoenix.PubSub.broadcast(Ebisu.PubSub, "bitbay_ticker", ticker)

    {:noreply, state}
  end

  defp schedule_ticker_add(state) do
    Process.send_after(self(), :add_ticker, Keyword.get(state, :interval, @interval))
  end
end
                        </code>
                        </pre>
                        <p>To test that GenServer sends message to itself, fetches data from external api and saves it to db we have to mock our http module. For that we use <a href="https://hexdocs.pm/mox/Mox.html">Mox library</a> and we specify mocked module in our setup. Next we start our worker and after 150ms we check db if we have new ticker.</p>
                        <pre><code>
defmodule Ebisu.Bitbay.Worker.TickerTest do
  use Ebisu.DataCase

  alias Ebisu.Bitbay.Worker.Ticker, as: TickerWorker
  alias Ebisu.Bitbay.Ticker

  import Mox

  setup [:verify_on_exit!, :set_mox_from_context]

  setup do
    http_client = Application.get_env(:ebisu, :http_client)

    Application.put_env(:ebisu, :http_client, Ebisu.Utils.MockHttp)

    on_exit(fn ->
      Application.put_env(:ebisu, :http_client, http_client)
    end)
  end

  test "inserts message" do
    expect(Ebisu.Utils.MockHttp, :get, 2, fn url ->
      if url == "https://bitbay.net/API/Public/BTCPLN/ticker.json" do
        %{
          "max" => 4500,
          "min" => 1465,
          "last" => 1533,
          "bid" => 1513,
          "ask" => 1542,
          "vwap" => 1524.42,
          "average" => 1545.67,
          "volume" => 4.54042857
        }
      else
        %{
          "table" => "A",
          "currency" => "dolar amerykaÅ„ski",
          "code" => "USD",
          "rates" => [
            %{"no" => "003/A/NBP/2021", "effectiveDate" => "2021-01-07", "mid" => 3.6656}
          ]
        }
      end
    end)

    start_supervised(%{
      id: TickerWorker,
      start: {TickerWorker, :start_link, [[interval: 100]]}
    })

    Process.sleep(150)

    assert Repo.aggregate(Ticker, :count) > 0
  end
end
                        </pre></code>
                        <p>Our web application consist of LiveView page which gets tickers at initial load and then passes tickers received from GenServer to web page where it is handled by javascript hook which updates graph.</p>
                        <pre><code>
defmodule EbisuWeb.TickerLive do
  use EbisuWeb, :live_view

  alias Ebisu.Bitbay
  alias Ebisu.Bitbay.Ticker, as: BitbayTicker

  @impl true
  def mount(_params, _session, socket) do
    if connected?(socket) do
      Phoenix.PubSub.subscribe(Ebisu.PubSub, "bitbay_ticker")
    end

    {:ok,
     assign(socket,
       tickers: %{
         bitbay: format(Bitbay.tickers())
       }
     )}
  end

  @impl true
  def render(assigns) do
    Phoenix.View.render(EbisuWeb.TickerView, "index.html", assigns)
  end

  @impl true
  def handle_info(%BitbayTicker{} = ticker, socket) do
    handle_new_ticker(ticker, socket, :bitbay)
  end

  defp handle_new_ticker(ticker, socket, type) do
    tickers =
      socket.assigns.tickers
      |> Map.get(type)
      |> add(ticker)
      |> window()

    tickers = Map.put(socket.assigns.tickers, type, tickers)

    socket = assign(socket, tickers: tickers)

    {:noreply, push_event(socket, "tickers", %{tickers: tickers})}
  end

  defp add(tickers, ticker) do
    tickers ++ [format(ticker)]
  end

  defp format(tickers) when is_list(tickers) do
    Enum.map(tickers, fn ticker -> format(ticker) end)
  end

  defp format(%BitbayTicker{} = ticker) do
    %{x: DateTime.to_time(ticker.updated_at), y: ticker.last / ticker.rate}
  end

  defp format(ticker) do
    %{x: DateTime.to_time(ticker.updated_at), y: ticker.last}
  end

  defp window(tickers) do
    Enum.take(tickers, -20)
  end
end
                        </code></pre>
                        <p>Our view consist of canvas definition where we store initial array of tickers. We also have to specify js hook using phx-hook.</p>
                        <pre><code>
&lt;canvas id=&quot;chart&quot; phx-update=&quot;ignore&quot; phx-hook=&quot;Chart&quot; data-tickers=&quot;&lt;%= Poison.encode!(@tickers) %&gt;&quot;&gt;&lt;/canvas&gt;
                        </pre></code>
                            <p>We define our hook in app.js file. In mounted function we specify how chart should look like. We also define handleEven which is used to update chart with new tickers.</p>
                        <pre><code>
let Hooks = {};
Hooks.Chart = {
    tickers() { return JSON.parse(this.el.dataset.tickers) },
    mounted() {
        let ctx = this.el.getContext('2d');
        let chart = new Chart(ctx, {
            type: 'line',
            data: {
                labels: this.tickers().bitbay.map(ticker => ticker.x),
                datasets: [{
                    label: 'Bitbay',
                    borderColor: 'rgb(255, 99, 132)',
                    data: this.tickers().bitbay.map(ticker => ticker.y)
                }]
            },
            options: {}
        });

        this.handleEvent("tickers", (data) => {
            chart.data.datasets[0].data = data.tickers.bitbay.map(ticker => ticker.y);
            chart.data.labels = data.tickers.bitbay.map(ticker => ticker.x);
            chart.update();
        });
    }
};

let csrfToken = document.querySelector("meta[name='csrf-token']").getAttribute("content")
let liveSocket = new LiveSocket("/live", Socket, {hooks: Hooks, params: {_csrf_token: csrfToken}})
                        </code></pre>
                    </div>
                </div>
    </body>
</html>
